/* Plugin Template generated by Pawn Studio */

#include <sourcemod>
#include <CodD0_engine>
#include <CodD0/skills/CodD0_skill_damage>
#include <CodD0/skills/CodD0_skill_longJump>

public Plugin myinfo =  {
	name = "CodD0 Class: Commando",
	author = "d0naciak",
	description =  "Commando",
	version = "1.0",
	url = "d0naciak.pl"
}

int g_classID;
bool g_hasClass[MAXPLAYERS + 1];
float g_roundStartTime;

public void OnPluginStart() {
	g_classID = CodD0_RegisterClass("Komandos", "Zabija z noża (PPM), użyj aby zrobić LongJump'a (moc +int)", {0, 25, 20, 100}, { CSWeapon_DEAGLE, CSWeapon_HEGRENADE, CSWeapon_FLASHBANG, CSWeapon_SMOKEGRENADE }, 4);

	HookEvent("round_start", ev_RoundStart_Post);
}

public void OnPluginEnd() {
	CodD0_UnregisterClass(g_classID);
}

public Action CodD0_ClassChanged(int client, int classID) {
	if(g_classID != classID && g_hasClass[client]) {
		CodD0_SetClientChanceToKillByKnife(client, CodD0_SkillSlot_Class, 0, false);
		CodD0_SetClientLongJumps(client, CodD0_SkillSlot_Class, 0, 0, 0.0);
		g_hasClass[client] = false;
	}
	return Plugin_Continue;
}

public void CodD0_ClassChanged_Post(int client, int classID) {
	if(g_classID == classID) {
		CodD0_SetClientChanceToKillByKnife(client, CodD0_SkillSlot_Class, 1, true);
		CodD0_SetClientLongJumps(client, CodD0_SkillSlot_Class, 99, 666, 4.0);
		g_hasClass[client] = true;

		float gameTime = GetGameTime(), skillGotReadyTime = g_roundStartTime + 5.0;
		if(skillGotReadyTime > gameTime) {
			CodD0_ClientClassSkillGotReady(client, skillGotReadyTime - gameTime);
		}
	}
}

public void CodD0_ClassSkillUsed(int client, int classID) {
	if(!g_hasClass[client]) {
		return;
	}

	if(CodD0_UseClientLongJump(client, CodD0_SkillSlot_Class) == CodD0_SkillPrep_Available) {
		CodD0_ClientClassSkillGotReady(client, 4.0);
	}
}

public ev_RoundStart_Post(Handle event, const char[] name, bool dontBroadcast) {
	g_roundStartTime = GetGameTime();

	for(int i = 1; i <= MaxClients; i++) {
		if (g_hasClass[i]) {
			CodD0_ClientClassSkillGotReady(i, 5.0);
		}
	}
}