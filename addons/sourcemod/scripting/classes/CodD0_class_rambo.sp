/* Plugin Template generated by Pawn Studio */

#include <sourcemod>
#include <sdkhooks>
#include <CodD0_engine>
#include <CodD0/skills/CodD0_skill_teleport>
#include <CodD0/skills/CodD0_skill_visibility>

public Plugin myinfo =  {
	name = "CodD0 Class: Rambo",
	author = "d0naciak",
	description =  "Rambo",
	version = "1.0",
	url = "d0naciak.pl"
}

int g_classID;
bool g_hasClass[MAXPLAYERS + 1];
float g_roundStartTime;

public void OnPluginStart() {
	g_classID = CodD0_RegisterClass("Rambo", "Może teleportować się na odległość 10m (+int) co 10 sekund, zmniejszona widoczność", {5, 30, 0, 25}, { CSWeapon_UMP45, CSWeapon_CZ75A, CSWeapon_FLASHBANG, CSWeapon_FLASHBANG }, 4);

	HookEvent("round_start", ev_RoundStart_Post);
}

public void OnPluginEnd() {
	CodD0_UnregisterClass(g_classID);
}

public Action CodD0_ClassChanged(int client, int classID) {
	if(g_classID != classID && g_hasClass[client]) {
		CodD0_SetClientTeleports(client, CodD0_SkillSlot_Class, 0, 0.0);
		CodD0_SetClientVisibility(client, CodD0_SkillSlot_Class, 255);
		g_hasClass[client] = false;
	}

	return Plugin_Continue;
}

public void CodD0_ClassChanged_Post(int client, int classID) {
	if(g_classID == classID) {
		CodD0_SetClientTeleports(client, CodD0_SkillSlot_Class, 99, 600.0 + RoundFloat(float(CodD0_GetClientUsableIntelligence(client)) * 4.0));
		CodD0_SetClientVisibility(client, CodD0_SkillSlot_Class, 90);
		g_hasClass[client] = true;

		float gameTime = GetGameTime(), skillGotReadyTime = g_roundStartTime + 10.0;
		if(skillGotReadyTime > gameTime) {
			CodD0_ClientClassSkillGotReady(client, skillGotReadyTime - gameTime);
		}
	}
}

public void CodD0_ClassSkillUsed(int client, int classID) {
	if(!g_hasClass[client]) {
		return;
	}

	if(CodD0_UseTeleport(client, CodD0_SkillSlot_Class) == CodD0_SkillPrep_Available) {
		CodD0_ClientClassSkillGotReady(client, 10.0);
	}
}

public ev_RoundStart_Post(Handle event, const char[] name, bool dontBroadcast) {
	g_roundStartTime = GetGameTime();

	for(int i = 1; i <= MaxClients; i++) {
		if (g_hasClass[i]) {
			CodD0_ClientClassSkillGotReady(i, 10.0);
		}
	}
}