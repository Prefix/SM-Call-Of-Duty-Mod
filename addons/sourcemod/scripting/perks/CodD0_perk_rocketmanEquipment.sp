/* Plugin Template generated by Pawn Studio */

#include <sourcemod>
#include <CodD0_engine>
#include <CodD0/skills/CodD0_skill_rocket>

public Plugin myinfo =  {
	name = "CodD0 Perk: Rocketman equipment",
	author = "d0naciak",
	description = "Rocketman equipment",
	version = "1.0",
	url = "d0naciak.pl"
}

int g_perkID;
float g_roundStartTime;

bool g_hasPerk[MAXPLAYERS + 1];

public void OnPluginStart() {
	g_perkID = CodD0_RegisterPerk("Zestaw Rakietnika", "Użyj, aby wystrzelić rakiety (LW na rundę)", 1, 3);

	HookEvent("round_start", ev_RoundStart_Post);
}

public void OnPluginEnd() {
	CodD0_UnregisterPerk(g_perkID);
}

public Action CodD0_PerkChanged(int client, int perkID, int perkValue) {
	if(g_perkID != perkID && g_hasPerk[client]) {
		CodD0_SetClientRockets(client, CodD0_SkillSlot_Perk, 0, 0.0, 0.0);
		g_hasPerk[client] = false;
	}
	
	return Plugin_Continue;
}

public void CodD0_PerkChanged_Post(int client, int perkID, int perkValue) {
	if (perkID == g_perkID) {
		CodD0_SetClientRockets(client, CodD0_SkillSlot_Perk, perkValue, 80.0, 0.5);
		g_hasPerk[client] = true;
		
		float gameTime = GetGameTime(), skillGotReadyTime = g_roundStartTime + 10.0;
		if(skillGotReadyTime > gameTime) {
			CodD0_ClientPerkGotReady(client, skillGotReadyTime - gameTime);
		}
	}
}

public void CodD0_PerkUsed(int client, int perkID) {
	if (!g_hasPerk[client]) {
		return;
	}

	if(CodD0_FireRocket(client, CodD0_SkillSlot_Perk) == CodD0_SkillPrep_Available) {
		CodD0_ClientPerkGotReady(client, 4.0);
	}
}

public ev_RoundStart_Post(Handle event, const char[] name, bool dontBroadcast) {
	g_roundStartTime = GetGameTime();

	for (int i = 1; i <= MaxClients; i++) {
		if (g_hasPerk[i]) {
			CodD0_ClientPerkGotReady(i, 10.0);
		}
	}
}